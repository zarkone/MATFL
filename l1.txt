<программа> 			→ <программа><описание функций> | <программа><описание переменных> | ε
<описание функций> 		→ void <id>()<блок> | int main()<блок>
<описание переменных> 	→ int <список переменных>; | __int64 <список переменных>;
<список переменных>		→ <элемент списка> | <список переменных>,<элемент списка>
<элемент списка>		→ <id><возможное присваивание> | <массив><возможная инициализация массива>
<массив>				→ <id>[<целая константа>]
<возможная 
инициализация массива> 	→ = {<список констант>} | ε
<список констант>		→ <целая константа>|<список констант>,<целая константа>
<возможное 
присваивание>			→ = <выражение> | ε
<id>					→ <буква><символы>
<буква>					→ a|...|Z
<символы>				→ <символы><буква> | <символы><цифра> | ε
<блок>					→ {<операторы>}
<операторы>				→ <операторы><оператор> | ε
<оператор>				→ <выражение>;|<for>|<блок>|<описание переменных>|<вызов функции>;|;


<выражение> → 	<A1>
				<A1>	→ <A2> = <A1> | <A2>
				<A2>	→ <A2> && <A3> | <A2> || <A3> | <A3>
				<A3>	→ <A3> > <A4> | <A3> >= <A4> | <A3> < <A4> | 
							<A3> =< <A4> | <A3> == <A4> | <A3> != <A4> | <A4>
				<A4>	→ <A4> + <A5> | <A4> - <A5> | +<A4> | -<A4> | <A5>
				<A5>	→ <A5> * <A6> | <A5> / <A6> | <A5> % <A6> | <A6>
				<A6>	→ (<A1>) | <id> | <переменная с индексом> | <целая константа>

<переменная с индексом> → <id>[<выражение>]
<вызов функции> 			→ <id>()
<целая константа>		→ <DEC константа> | <HEX константа> | 0
<DEC константа>			→ <цифры>
<цифры>					→ <цифры><цифра> | <цифра>
<цифра>					→ 0|...|9
<HEX константа>			→ 0x<HEX цифры>
<HEX цифры>				→ <HEX цифры><HEX цифра> | <HEX цифра> 
<HEX цифра>				→ 0|...|9|A|...|F

<for>	→ for (<выражение>; <выражение> ; <выражение>)<оператор>